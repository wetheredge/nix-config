{
  config,
  lib,
  ...
}: let
  cfg = config.services.authelia.instances.main;
  service = "authelia-${cfg.name}";
  stateDir = "/var/lib/${service}";
  database = "${stateDir}/db.sqlite3";
  socket = "/var/run/${service}/authelia.sock";

  redisCfg = config.services.redis.servers.authelia;

  rootDomain = "wetheredge.com";
  domain = "auth.${rootDomain}";

  getSecretRaw = name: ''(fileContent "${config.age.secrets.authelia.path}" | fromYaml).${name}'';
  getSecret = name: ''{{ ${getSecretRaw name} }}'';
  getSecretBlock = depth: name: ''{{ ${getSecretRaw name} | nindent ${toString (2 * depth)} }}'';
  # Nix quotes go templates in the yaml generated by `cfg.settings`, which breaks multiline template outputs
  settings = ''
    theme: auto

    log:
      format: text
      level: info

    server:
      address: 'unix://${socket}?umask=0111'

    authentication_backend:
      file:
        path: '${config.age.secrets.authelia-users.path}'

      # User database is read-only
      password_change:
        disable: true
      password_reset:
        disable: true

    access_control:
      default_policy: two_factor

    session:
      secret: '${getSecret "session_secret"}'
      cookies:
        - domain: '${rootDomain}'
          authelia_url: 'https://${domain}'
      redis:
        host: '${redisCfg.unixSocket}'
        password: '${getSecret "redis_password"}'

    storage:
      encryption_key: '${getSecret "storage_key"}'
      local:
        path: '${database}'

    notifier:
      filesystem:
        filename: '${stateDir}/notification'

    identity_providers:
      oidc:
        hmac_secret: '${getSecret "oidc.hmac_secret"}'
        jwks:
          - key: |-
              ${getSecretBlock 5 "oidc.rsa_key"}
        clients:
          - client_name: Forgejo
            client_id: '${getSecret "oidc.client.forgejo.id"}'
            client_secret: '${getSecret "oidc.client.forgejo.secret"}'
            redirect_uris:
              - 'https://git.wetheredge.com/user/oauth2/authelia/callback'
            pre_configured_consent_duration: '6 months'
            require_pkce: true
            pkce_challenge_method: S256
          - client_name: Linkding
            client_id: '${getSecret "oidc.client.linkding.id"}'
            client_secret: '${getSecret "oidc.client.linkding.secret"}'
            redirect_uris:
              - 'https://links.wetheredge.com/oidc/callback/'
            scopes:
              - openid
              - email
              - profile
            pre_configured_consent_duration: '6 months'
            token_endpoint_auth_method: client_secret_post
          - client_name: Miniflux
            client_id: '${getSecret "oidc.client.miniflux.id"}'
            client_secret: '${getSecret "oidc.client.miniflux.secret"}'
            redirect_uris:
              - 'https://miniflux.wetheredge.com/oauth2/oidc/callback'
            scopes:
              - openid
              - email
              - profile
            pre_configured_consent_duration: '6 months'
  '';
in {
  services.authelia.instances.main = {
    enable = true;

    settingsFiles = [(builtins.toFile "authelia.yaml" settings)];
    secrets.manual = true;
    environmentVariables = {
      X_AUTHELIA_CONFIG_FILTERS = "template";
    };
  };

  systemd.services.${service}.serviceConfig = {
    RuntimeDirectory = service;
  };

  services.redis.servers.authelia = {
    enable = true;
    requirePassFile = config.age.secrets.redis-authelia-password.path;
    unixSocketPerm = 666;
  };
  systemd.services.redis-authelia.serviceConfig = {
    RuntimeDirectoryMode = lib.mkForce 777;
  };

  age.secrets =
    lib.mapAttrs (_: cfg: {
      owner = cfg.user;
      inherit (cfg) group;
    }) {
      authelia = cfg;
      authelia-users = cfg;
      redis-authelia-password = redisCfg;
    };

  services.caddy.virtualHosts = {
    ${domain}.extraConfig = "reverse_proxy unix/${socket}";
  };

  preservation.preserveAt.state.files = [
    {
      file = database;
      inherit (cfg) user group;
    }
  ];
}
